plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
}

ext {
//	set('springCloudVersion', "2021.0.9")
	set('springCloudVersion', "2022.0.4")
	set('testcontainersVersion', "1.19.8")
	set('otelVersion', "1.33.3")
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
	implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-reactor-resilience4j'
	implementation 'org.springframework.session:spring-session-data-redis'

	// 라우트 확인을 위해 의존성 추가
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	// 스프링 보안 의존성 추가
	implementation 'org.springframework.boot:spring-boot-starter-security'
	// 스프링 보안 OAuth2 클라이언트 의존성 추가
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'


	// Only on Apple Silicon. Why it's necessary: https://github.com/netty/netty/issues/11020
 	runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.1.101.Final:osx-aarch_64'
	runtimeOnly 'io.github.resilience4j:resilience4j-micrometer'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	runtimeOnly "io.opentelemetry.javaagent:opentelemetry-javaagent:${otelVersion}"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.testcontainers:junit-jupiter'
}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

// 스프링 부트에서 제공하는 빌드팩을 이용해 이미지 만드는 플러그인 설정 시작
bootBuildImage {
	builder = "docker.io/paketobuildpacks/builder-jammy-base"
	imageName = "${project.name}" // 그래들의 프로젝트 설정에서 정의한 이름과 같음. 버전 번호를 사용하지 않고 암묵적인 latest 태그를 사용한다.
	environment = ["BP_JVM_VERSION": "17"] // 이미지에 설치할 JVM 버전

	// 컨테이너 저장소 인증을 설정하기 위한 섹션. 값은 그래들 속성을 통해 전달됨
	docker {
		publishRegistry {
			username = project.findProperty("registryUsername")
			password = project.findProperty("registryToken")
			url = project.findProperty("registryUrl")
		}
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
